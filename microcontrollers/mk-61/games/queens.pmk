; 8 ферзей — Сергей Тарасов
; ~~~~~~~~~~~~~~~~~~~~~~~~~
; (http://www.arbinada.com/pmk/node/26)
;
; Поиск расстановки 8 ферзей на шахматной доске используется для
; измерения скорости вычислений. Перед ПМК стоит задача найти
; первую подходящую расстановку.
;
; Очевидно, на каждой из 8 вертикалей должно стоять по ферзю.
; Каждую такую расстановку можно закодировать одномерным массивом
; X[1],...,X[8], где X[i] - номер горизонтали для i-го ферзя.
;
; Поскольку никакие два ферзя не могут стоять на одной
; горизонтали (тогда они бьют друг друга), то все X[i] различны,
; т.е. образуют перестановку из чисел 1..8.
;
; Инструкции по запуску: В/0, С/П
;
; После останова в регистрах Р1..Р8 координаты по вертикали i-го
; ферзя (стоящего на i-й горизонтали).
;
; Контрольный запуск на эмуляторе выдает следующую комбинацию:
; Р1=8, Р2=4, Р3=1, Р4=3, Р5=6, Р6=2, Р7=7, Р8=5,
; что соответствует расстановке на шахматной доске:
; Фa8, Фb4, Фc1, Фd3, Фe6, Фf2, Фg7, Фh5
;
; На реплике калькулятора МК-61 (pic32mx2 48MHz)
; время выполнения составило 1 час 19 минут 30 секунд.
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 00
        Cx
        хПB
        9
        хП0
04:     Cx
        КхП0
        Пх0
        Fx=0
            04
        хПС
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 10
10:     ПхС
        1
        +
        хПС
        8
        КхПС
16:     ПхВ
        1
        +
        хПВ
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 20
        ПхС
        хПД
22:     ПхД
        1
        -
        Fx≠0
            56
        Fx≥0
            56
        хПД
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 30
        КПхС
        КПхД
        -
        Fx≠0
            42
        К|x|
        ПхС
        ПхД
        -
        -
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 40
        Fx=0
            22
42:     0
        хПВ
44:     КПхС
        1
        -
        КхПС
        Fx=0
            22
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 50
        ПхС
        1
        -
        хПС
        БП
            44
56:     ПхВ
        1
        -
        Fx=0
;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;;; 60
            16
        ПхС
        8
        -
        Fx=0
            10
        С/П
